r←{flag} FetchAdditionalDataForListPackages path;cfg;OSes;f;rns
 flag←{0::0⋄flag}⍬
 cfg←ReadPackageConfigFile path
 OSes←⊃{0=≢⍵:'' ⋄ {⍺,', ',⍵}/⍵}cfg.(os_lin os_mac os_win)/'Lin' 'Mac' 'Win'
 :If 0=cfg.⎕NC'project_url'             ⍝ Was introduced after packages were already published
     r←cfg.description''OSes
 :Else
     r←cfg.(description project_url),⊂OSes
 :EndIf
 :If f←0<cfg.⎕NC'userCommandScript'     ⍝ Was introduced after packages were already published
     f←0<≢cfg.userCommandScript
 :EndIf
 r,←⊂f/'Yes'                            ⍝ Mark as a user command
 r,←⊂cfg.tags
 :If flag
    r,←⊂cfg.date
    r,←⊂cfg.name
    r,←⊂cfg.group
    r,←⊂cfg.license
    r,←⊂cfg.version
    rns←⎕ns''
    rns.(desc projURL supports UC tags date name group license version)←r
    r←rns
 :EndIf

⍝Done
